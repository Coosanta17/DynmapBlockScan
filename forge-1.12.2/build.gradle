buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        
        maven {
            name = "forgeFS"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'eclipse'

sourceSets {
    main.java.srcDirs = ['src/main/java', '../core/src/main/java', '../core/src/main/java']
    main.resources.srcDirs = ['src/main/resources', '../core/src/main/resources', '../core/src/main/resources']
}

eclipse {
    project {
        name = "DynmapBlockScan(Forge-Legacy-1.12.2)"
    }
}

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${parent.version}"
group= "us.dynmap.forge" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "DynmapBlockScan"
targetCompatibility = '1.8'
sourceCompatibility = '1.8'
ext.buildNumber = System.getenv().BUILD_NUMBER ?: "Dev"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
	runDir = "run"
	mappings = 'snapshot_nodoc_20171021'
	replaceIn "Version.java"
    	replace "@VERSION@", parent.version + '-' + project.ext.buildNumber
    	replace "@BUILD_NUMBER@", project.ext.buildNumber
}

repositories {
    mavenLocal()
    maven { url = "https://repo.mikeprimm.com" }
}

configurations {
    dynmap
    compile.extendsFrom dynmap
}

dependencies {
    dynmap "us.dynmap:DynmapCoreAPI:${parent.version}:all"
    implementation project(path: ":core", configuration: "shadow")
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand(
        	version: parent.version + '-' + project.ext.buildNumber,
        	mcversion: config.minecraft_version
    	)
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    // Move access transformer to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}


shadowJar {
    dependencies {
        include(dependency(':core'))
        exclude(dependency("us.dynmap:DynmapCoreAPI"))
        exclude("META-INF/maven/**")
        exclude("META-INF/services/**")
        exclude("com/electronwill/**")
    }
    archiveName = "${parent.name}-${parent.version}-forge-1.12.2.jar"
    destinationDir = file '../target'
}

shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}


reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives deobfJar
}

build.dependsOn(shadowJar)

